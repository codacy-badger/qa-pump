plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'pmd'
    id 'checkstyle'

    id 'com.github.spotbugs' version '3.0.0'
    id 'info.solidsoft.pitest' version '1.4.7'
    id 'io.freefair.lombok' version '5.0.0-rc4'
    id 'com.github.kt3k.coveralls' version '2.10.1'
}
apply from: './gradle/dependencies.gradle'
apply from: './gradle/checking.gradle'
apply from: './gradle/testing.gradle'
apply from: './gradle/setup.gradle'
subprojects {
    apply plugin: 'java'
}
version = theVersion
group = theGroup
archivesBaseName = theArchivesBaseName

compileJava {
    options.encoding = 'UTF-8'
}
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
idea {
    module {
        downloadJavadoc = true
        downloadSources = false
    }
}
wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.2.2'
}
generateLombokConfig.enabled = false
clean {
    doLast {
        def deleting = files("out", "allure-results")
        println "Extra cleaning: $deleting.files"
        delete deleting
    }
}